// module
#uselib "shell32"
    #func ExtractIcon "ExtractIconA" int, sptr, int
#uselib "user32"
    #func DrawIcon "DrawIcon" int, int, int, int
#uselib "gdi32"
    #cfunc CreateDC "CreateDCA" sptr, sptr, sptr, int
    #func DeleteDC "DeleteDC"  int
    #func BitBlt "BitBlt" int, int, int, int, int, int, int, int, int
#include "KeyHook.hsp"
#include "a2d.hsp"

// definition
#define SRCCOPY         0x00CC0020
#define CAPTUREBLT      0x40000000
iconHalfSize = 16
radOffset = M_PI/2
dx = ginfo_dispx
dy = ginfo_dispy

// pre-process
appPath = "C:\\Program Files\\Google\\Chrome\\Application\\chrome.exe", "C:\\Program Files (x86)\\LG Electronics\\OnScreen Control\\bin\\OnScreen Control.exe", "C:\\hsp351\\hdl.exe", "C:\\Users\\lconn\\AppData\\Local\\GitHubDesktop\\GitHubDesktop.exe"
trigger = 29  // muhenkan
keySwitch = 0
blend = 150
transition = -1
depth = 5
appR = 0.6 * dy/2

appNum = length(appPath)
dim iconHandle, appNum
repeat appNum
    ExtractIcon hInstance, appPath(cnt), 0
    IconHandle(cnt) = stat
    if (stat == 0) : dialog "loading failed."
loop

khHookLL *onKeyHook

// screen
bgscr 0, dx, dy, screen_hide, 0, 0  // main
    color : boxf
buffer 1, dx, dy                    // for screen shot
buffer 2, dx, dy                    // for blended screen
gsel 0, 0

// label
*main
    redraw 0
        if (keySwitch == 0) {
            transition = -1
            gsel 0, -1
        } else : if (keySwitch == 1) {
            if (transition == -1) {
                gsel 1, 0
                hdcScreen = CreateDC("DISPLAY", 0, 0, 0)
                BitBlt hdc, 0, 0, dx, dy, hdcScreen, 0, 0, SRCCOPY | CAPTUREBLT
                DeleteDC hdcScreen
                gsel 0, 0
                gmode
                pos 0, 0 : gcopy 1, 0, 0, dx, dy
                transition = 0
                gsel 0, 2
            }
            if (transition <= depth) {
                gsel 0, 0
                gmode gmode_alpha, dx, dy, blend
                pos 1, 0 : gcopy 0
                pos 0, 0 : gcopy 0, 2, 0
                pos 0, 1 : gcopy 0
                pos 0, 0 : gcopy 0, 0, 2
                gsel 1, 0
                gmode
                pos 0, 0 : gcopy 0, 0, 0, dx, dy
                gsel 0, 0
                transition++
            } else : if (transition > depth) {
                color $80, $80, $80
                gsel 0, 0
                gmode
                pos 0, 0 : gcopy 1, 0, 0, dx, dy
                circle dx/2-appR, dy/2-appR, dx/2+appR, dy/2+appR, 0
                repeat appNum
                    DrawIcon hdc, dx/2 + appR*cos(2.0*M_PI/appNum*cnt - radOffset)-iconHalfSize, dy/2 + appR*sin(2.0*M_PI/appNum*cnt - radOffset)-iconHalfSize, IconHandle(cnt)
                loop
            }
        }
    redraw 1
    await
    goto *main

*onKeyHook
    if (khKeyCode == trigger) {
        if ((khStat == 0) & (keySwitch == 0)) {
            keySwitch = 1
        } else : if ((khStat == 2) & (keySwitch == 1)) {
            keySwitch = 0
        }
    } else : if (khKeyCode == 27) {  // for debug
        end
    }
    return